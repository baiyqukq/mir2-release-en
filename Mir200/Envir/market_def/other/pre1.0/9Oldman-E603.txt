;物价
%200

;对话, '*' : 对话tag

;;------------------------------------------------------------------------------
;; [310] 接到寻找老人的指示的人物
;; [311] 寻找鹿血和鹿茸
;; [312] 找到鹿血和鹿茸的人
;;------------------------------------------------------------------------------

[@main]
#IF 
random 2
#ACT
goto @QUEST
#ELSEACT
goto @normal

[@QUEST]
#IF 
check [312] 1
#ACT
goto @Q312
break

#IF
check [311] 1
#ACT
goto @Q311
break

#IF
check [351] 1
#ACT
goto @Q351
break

#IF
check [310] 1
random 5
#ACT 
goto @Q310
#ELSEACT
goto @normal

[@normal]
嗯.. 你怎么会来到这么危险的地方？\
我不敢说你能不能从这儿走出去。\ \

  <进行对话/@talkwith_1>\
  <关闭./@exit>\

[@Q310]
#IF
#SAY
你有什么事情吗？要我制造特殊油给你，是吗？\ 
要制造这种油，需要稀有材料。\
如果你能找到这些材料，我就给你制造。\ 
这些材料包括鹿茸和鹿血。\
如果在别的地方找不到这些材料， \ \
<下一个/@Q310_1>

[@Q310_1]
#IF
#ACT
SET [311] 1
#SAY
我听说鹿茸只能在地下才能够找得到。\
你知道怎么去吗？如果不知道，那我也没办法。\ 
至于鹿血我就不清楚在哪儿可以得到，\ 
祝你好运。\ \
 <关闭/@exit>

[@Q311]
#IF
check [311] 1
checkitem 鹿茸 1
checkitem 鹿血 1
#ACT
goto @Q311_1
#ELSEACT
goto @Q311_2

[@Q311_1]
#IF
#ACT
SET [312] 1
take 鹿茸 1
take 鹿血 1
#SAY
你真棒，那么难找的材料都能够找到。\
我会把制造的油交给比奇肉店的屠夫，\
你去看看吧。\ \
去<等@exit>

[@Q311_2]
#IF
#SAY
还没能找到啊，快去找吧。\
我不是好脾气，等不了那么长时间。\ \
<关闭/@exit>

[@Q312]
#IF
#SAY
快去找比奇省的屠夫吧。 \ \
<关闭/@exit>

[@talkwith_1]
#IF
dayofweek Sunday
hour 20 20
#ACT
goto @talkwith_2
break

#IF
  dayofweek Saturday
  hour 20 20
#ACT
goto @talkwith_2
#ELSEACT
goto @untalkwith
   
[@talkwith_2]
你跑这么老远和我搭话， \
我就告诉你一个好情报。\
这附近有一个无人知晓的洞穴，\
这个洞穴和传奇世界不一样，我也只是听说过一次。\
一次也没去过，但是我可以给你带路。\ \ 

<挑战/@talkwith_3>\
<关闭/@exit>

[@talkwith_3]
嗨，我可以让你进去，\
但是记住，过三个小时之后你会回到原来的地方，那你小心点。\ \
<进入/@talkwith_4>
   
[@talkwith_4]
#IF
#ACT
TimeRecall 180
map M001


[@Q351]
#IF
   random 5
#ACT
goto @Q351_1
#ELSEACT
goto @normal

[@Q351_1]
#IF
#SAY
你就是那个出了名的臭脾气吗？\
你和我脾气差不多，好。哈哈哈.. \
你想不想赚一次大钱？ \ \
<想赚钱/@Q351_1_1> \
<不想赚钱/@Q351_1_2>

[@Q351_1_1]
哦，想赚是吧？ \
好，那你带着斩马刀去找地牢二层的奸商小李吧，\
他会告诉你答案的。\ \
<去找/@Q351_1_1_1>

[@Q351_1_2]
听说你脾气不好，看来也不仅仅是这样啊。\
我都不好意思看你，\
回去练好修养吧。\ \
<关闭/@exit>

[@Q351_1_1_1]
#IF
#ACT
  SET [352] 1
#SAY 
祝你好运。\ \
<关闭/@exit>

[@untalkwith]
#IF 
   random 10
#SAY 
你有没有听说过地下？ \
我也不能向你仔细说明，\
但是可以告诉你进入的时间是周末的晚上， \
这个时间你再来找我的话， \
我可以带你到那个地方，\ 
那到时候见吧。\ \
<返回/@main>
#ELSESAY
和你说什么？\
快从这儿消失吧。\ \
<返回/@main>

