%100
;戒指
+22
+23
;; 命运之刃指示第一分歧点
;###################################
;;惹恼卫家店时 [210]
;;惹恼戒指店老板时[211]
[@main]
#IF
check [210] 1
check [211] 1
#ACT
goto @notalk
break
;;与戒指店老板讲话途中，重新找到金矿时 [212]
#IF
check [210] 1
check [212] 1
#ACT
goto @heard
break
;;惹恼卫家店时 [210]
#IF
check [210] 1
#ACT
goto @talk_1
break
;;给张氏赊账时 [215]
#IF
check [215] 1
#ACT
goto @bollow
break
;;指示分歧还没有开始时
#ELSEACT
goto @main_1

;###################################

;对话, '*' : 对话tag

[@main_1]
欢迎光临，有什么事情吗？\ \
<买/@buy>戒指\
<卖/@sell>戒指\
<修理/@repair>戒指\
<关闭/@exit>
<>

[@buy]
要买戒指吗？\ \ \
 <返回/@main_1>

[@sell]
要卖戒指吗？\ \ \
 <返回/@main_1>

[@repair]
要修理戒指吗？\ \ \
 <返回/@main_1>

[~@repair]
修理得还算不错。\ \
 <返回/@main_1>

;惹恼匠人时

[@talk_1]
哎呀，听说惹恼了卫家店？\
他虽然做的是粗活，\
但是性格却格外的小气，\
只有我才有办法哄他不生气，\
怎么样，想听听吗？\ \
 <听/@heard>\
 <拒绝/@noheard>

[@heard]
哄他不生气的办法很简单，\
我想他肯定是要你白给他金矿，而你没给，对吗？\
我会哄他的。\
只要你给我5个金矿。\ \
 <给金矿/@heard_1>\
 <拒绝/@noheard>

[@heard_1]
#IF
checkitem 金矿 5
#ACT
take 金矿 5
#SAY
请稍等，我马上来。\ \
 <过一会儿/@appease>
#ELSESAY
没有金矿，快去找回来。\ \
 <关闭/@exit>
#ELSEACT
set [212] 1

[@noheard]
#IF
#ACT
set [211] 1
#SAY
原来匠人生气有理由啊。\
这样对待很亲切的人的人，我是第一次见。\
哼，我也不想和你多说。\
你走吧。\ \
 <关闭/@exit>

[@notalk]
#IF
RANDOM 5
#ACT
set [211] 0
#SAY
既然你这样讲话，那我就原谅你吧。\
但是你不能像对我一样对待卫家店，\
他和我不一样。他是很小心眼的。\ \
 <好的/@heard>

#ELSESAY
我不想和像你这样的人再讲下去。\
马上出去!!\ \
 <关闭/@exit>

[@appease]
#IF
#ACT
set [210] 0
set [213] 1
#SAY
现在好了，你可以去看看卫家店。\ \
 <关闭/@exit>

[@bollow]
#IF
check [216] 0
#SAY
啊，你是帮助卫家店的那位。\
我听说过你。\
你认识现在住在比奇省的铁匠铺老板吗？\
虽然现在看起来很寒酸，\
但是以前可是很有名气的匠人，现在是喝酒喝成那个样子。\
我替你和比奇省铁匠铺老板讲，你去找他吧。\ \
 <下一个/@next>
#ELSESAY
快去找比奇省铁匠铺老板吧。\ \
 <关闭/@exit>
#ELSEACT
set [215] 0

[@next]
#IF
check [216] 0
#ACT
reset [210] 10
set [214] 1
set [215] 1
set [216] 1
give 金币 5000
#SAY
哎，卫家店把赊账的金矿价钱交给我了，\
给你，这是500分。\
他说他只能给你这些。\ \
 <关闭/@exit>

[goods]
;商品          数量       时间
古铜戒指        3          3
六角戒指        3          3
玻璃戒指        3          3

