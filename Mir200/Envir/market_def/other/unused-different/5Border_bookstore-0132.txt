;/R => remark as read
;  scrolls, books
%80
+3
+4

;Dialogue, '*' : Dialogue tag
[@main1]
欢迎，你想买些武技修炼的书吗？\ \
 <买/@buy>  书\
 <卖/@sell>  书 \
 <询问/@helpbooks> 各种武技的细节 \
 <交谈/@talk>\
 <取消/@exit>
<>

[@buy]
请选择你想要买的书。\ \ \
 <返回/@main1>

[@sell]
给我看看你的书。\ \ \
 <返回/@main1>

[@helpbooks]
你对那种书感兴趣？\ \
武士可以学<基本剑术/@help11>和<攻杀剑术/@help12>\
魔法师可以学<火球术/@help21>和<爆裂火焰/@help23>
道士可以学<治愈术/@help31>,<精神力战法/@help32>和\
<施毒术/@help33>\ \
 <返回/@main1>

[@help11]
基本剑术你可以按如下步骤修炼：\
Lv.1 : 你可以在第 7 级开始修炼\
Lv.2 : 你可以在第 11 级开始修炼\
Lv.3 : 你可以在第 16 级开始修炼\\
 <返回/@helpbooks>


[@help12]
攻杀剑术你可以按如下步骤修炼：\
Lv.1 : 你可以在第 20 级开始修炼。\
Lv.2 : 你可以在第 22 级开始修炼。\
Lv.3 : 你可以在第 24 级开始修炼。\\
 <返回/@helpbooks>


[@help21]

火球术你可以按如下步骤修炼：\
Lv.1: 你可以在第 7 级开始修炼。\
Lv.2: 你可以在第 11 级开始修炼。\
Lv.3: 你可以在第 16 级开始修炼。\\
 <返回/@helpbooks>

[@help23]
爆裂火焰你可以按如下步骤修炼：\
Lv.1: 你可以在第 22 级开始修炼。\
Lv.2: 你可以在第 27 级开始修炼。\
Lv.3: 你可以在第 31 级开始修炼。\\
 <返回/@helpbooks>


[@help31]
治愈术你可以按如下步骤修炼：\
Lv.1 : 你可以在第 7 级开始修炼.\
Lv.2 : 你可以在第 11 级开始修炼.\
Lv.3 : 你可以在第 16 级开始修炼. \\
  <返回/@helpbooks>


[@help32]
精神力战法你可以按如下步骤修炼：\
Lv.1 : 你可以在第 9 级开始修炼.\
Lv.2 : 你可以在第 13 级开始修炼.\
Lv.3 : 你可以在第 19 级开始修炼.\\
  <返回/@helpbooks>

[@help33]
施毒术你可以按如下步骤修炼：\
Lv.1 : 你可以在第 14 级开始修炼.\
Lv.2 : 你可以在第 17 级开始修炼.\
Lv.3 : 你可以在第 20 级开始修炼.\\
 <返回/@helpbooks>

[@talk]
我是这里的学者兼教师,我对我的学生很严厉\
因为我想让他们尽快的成材\\
 <再多聊聊/@new>\
 <返回/@main1>\\

[@new]
#if
#act
goto @news1

[@news1]
#if
checkitem 佛牌 1
#act
goto @quest
#elseact
goto @normal

[@normal]
而我这里的藏书算是应有尽有了,但还是比比奇城堡差了些\
比奇城堡的书店是由一对双生兄弟所经营的店铺\
如果这里没有你要的书,你可以去那里看看\\
<返回/@main1>

[@quest]
#if
checklevel 11
#act
goto @new2_1
#elseact
goto @new2_2

[@new2_1]
#if
#act
take 佛牌 1
#say
这是我那屠夫学生送给我的佛牌吗?\
想不到他也会想的到我....\
<$USERNAME>,谢谢你啊!\
这块佛牌我收下了!\\
<确定/@exit>

[@new2_2]
#if
#act
take 佛牌 1
#say
这是我那屠夫学生送给我的佛牌吗?\
想不到他也会想的到我....\
真是谢谢你了!\
这块佛牌我收下了\
但今晚的晚宴还缺一些材料啊\
你能帮我找来吗?\\

<可以/@new2_21>,我很乐意帮助你\
<抱歉/@new2_22>,我很忙,帮不了你\

[@new2_22]
真是可惜了\
那再见了\
<再见/@exit>

[@new2_21]
真的吗?太好了非常感谢你啊\
请帮我找5块品质12以上的肉\
和5块品质5以上的鸡肉\
如果找到的话请尽快给我\

<接受/@new2_211>\
<太难了/@new2_212>,我完成不了

[@new2_212]
这样啊\
那我还是找别人帮忙吧!佛牌我就拿走了\

[@new2_211]
在这个村子周围有很多鹿啊,鸡啊\
你应该能在这里得到足够的肉\
<确定/@exit>

[@main]
#if
checkitem 肉 5
checkitem 鸡肉 5
checkduraeva 肉 12
checkduraeva 鸡肉 5
#act
take 肉 5
take 鸡肉 5
give 金币 1000
goto @new3
#elseact
goto @main1

[@new3]
#if
checklevel 11
#act
goto @new3_1
#elseact
goto @new3_2

[@new3_1]
#if
#say
多谢你送给我这些肉\
这些钱是给你的报酬\
请务必收下\\
<确定/@exit>

[@new3_2]
#if
#say
年轻人\
真是太感谢你了\
<$USERNAME>\
这个名字将来一定会传遍整个大陆的\
这是我家代代相传的一条传统项链\
送给你做个纪念吧!\

<接受/@new3_21>\
助人为快乐之本,<我不能要/@new3_22>

[@new3_21]
#if
#act
give 传统项链 1
#say
戴上它吧\
希望能对你有所帮助\
不要再留恋这个小村庄了\
勇敢的青年啊!快去创造奇迹!\\
<再见/@exit>

[@new3_22]
#if
#act
give 祈福项链 1
#say
真的吗?\
你是我这么多年来见过的心肠最好的人了!\
既然你执意如此,我也不勉强你了\
我想你一定愿意再帮我一次.\
我有一个侄子在比奇城飞天武馆教人武功\
你帮我把这条祈福项链送给他吧!\
千万不要丢在路上啊!\\
<询问/@ask>武馆的位置\
<确定/@exit>

[@ask]
飞天武馆在比奇城的右上方\
比奇城在比奇省的中部\
坐标300,300左右的地方\
祝你一路顺风!这样我再给你点路费,你就不要推辞了\\
<收取路费/@lufei>


[@lufei]
#if
#act
give 金币 180
#say
收好,不要给人抢了哦\\
<再见了/@exit>

[goods]
;items                   quantity       minute
火球术       100            1
治愈术       100            1
基本剑术     100            1
精神力战法   100            1
抗拒火环     100            1
刺杀剑术     20             1
攻杀剑术     20             1
雷电术     20             1
