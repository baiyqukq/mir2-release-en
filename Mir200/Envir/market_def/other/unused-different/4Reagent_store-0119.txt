;武器
%100
+0
+42

;---------------------------------------------------------
;
;[104] 是否送到药剂师的信
;[105] 和许中医做好朋友
;[106] 答应给找食人树的果实
;[107] 给充分的食人树的果实
;[108] 收到许中医第一个药
;
;----------------------------------------------------------

;对话, '*' : 对话tag
[@main]
欢迎光临，有什么事情需要我帮忙吗？\ \
 <买/@buy>药\
 <卖/@sell>药\
 <进行对话/@talkwith>\
 <关闭/@exit>
<>

[@buy]
需要什么东西？\ \ \
 <返回/@main>

[@sell]
请给要卖的东西。我会给好价钱的。\ \ \
 <返回/@main>

;#####################################################

[@talkwith]
#IF
unit  [1] 0
#ACT
goto  @talkwith_1


;#####################################################


[@talkwith_1]
;讲有关信件的事情
#IF
check [103] 1
check [104] 0
check [105] 0
#ACT
goto @talk_about_letter
break

;为了成为朋友，再回来
#IF
check  [103]  1
check  [104]  1
check  [106]  0
#ACT
goto @talk_1_1
break

;来给食人树的果实
#IF
check  [104]  1
check  [106]  1
check  [107]  0
#ACT
goto @talk_2_1
break

;为得到许中医的药而来
#IF
check  [104]  1
check  [106]  1
check  [107]  1
check  [108]  0
#ACT
goto 	@talk_3_1
break

;没有任何条件时的对话
#IF
check  [105] 1
#SAY
<$USERNAME> 对不起，我现在太忙。\
可不可以下次再来?\
很抱歉。\
#ACT
break

#IF
#SAY
对不起，我现在忙，以后再来找我吧。\


;-------------------------------------------------------
;为传达信件而来
;-------------------------------------------------------

[@talk_about_letter]
...就是说你带着我父亲的信件，\
因为什么事情给我写信的呢？\
快把信交给我。\ \
<传达信件/@give_letter>\
<信件丢了/@no_give_letter>\

[@give_letter]
#IF
CHECKITEM  药剂师的信 1
#ACT
TAKE  药剂师的信  1
SET   [104]   1
#SAY
谢谢，让你辛苦了。\
真太谢谢了，能告诉名字吗？\
我是许中医。\
你叫什么？\ \
<告诉名字/@say_my_name>\
#ELSESAY
现在没有信?\
你是说在路上丢了？找到之后马上给我。\

[@no_give_letter]
什么？把信丢了？\
呵呵，好了，那也用不着这么客气，\
这样一来我就变得不好意思了，\
好了，没事了，你回去吧\

[@say_my_name]
<$USERNAME> 嗨, 见到你很高兴。\
这也是缘分，我们友好相处吧。\
这是我的一点心意，收下吧。\
是叫做太阳神水的药水，很管用的。\ \
<收下/@ok_take_drug>

[@ok_take_drug]
#ACT
SET  [105]    1
GIVE 强效太阳水 1
#SAY
对了，下次一定再来。\
再见\

;-------------------------------------------------------
; 传达信之后来。给食人树的果实之前
;-------------------------------------------------------

[@talk_1_1]
#SAY
啊 <$USERNAME>, 欢迎光临 \
你过的还好吗？\
现在我需要食人树的果实\
如果你帮我弄到几个，我会给你好价钱的。\
请你一定帮我。\
那我就等你的好消息了。\
#ACT
SET  [106] 1

;-------------------------------------------------------
; 为交给食人树的果实而来
;-------------------------------------------------------

[@talk_2_1]
#IF
CHECKITEM  食人树的果实  1
#SAY
还是你好，没有忘记。\
拿来的话，就一个一个给吧。\ \
<给/@give_sg_1>\
#ELSEACT
GOTO  @talk_1_1

[@give_sg_1]
#IF
CHECKITEM  食人树的果实 1
#ACT
TAKE 食人树的果实 1
GIVE 金币 600
GOTO @check_enough_1

;检查是否充分收到食人树的果实
[@check_enough_1]
#IF
RANDOM 10
DAYTIME SUNSET
#ACT
SET  [107] 1
#SAY
谢谢你 <$USERNAME> 现在食人树的果实已经够了。\
这都是你的功劳。\
现在我要开始炼药了，\
请你为我祈祷吧。\
再见\
#ELSEACT
GOTO @give_sg_2

[@give_sg_2]
#IF
CHECKITEM 食人树的果实 1
#ACT
GOTO @talk_2_1
#ELSEACT
CLOSE


;-------------------------------------------------------
;许中医正在炼第一个药
;-------------------------------------------------------

[@talk_3_1]
#if
daytime day
#act
goto 	@talk_3_2
#elsesay
<$USERNAME>, 欢迎光临，我的药还没有炼成。\
要一旦炼成了，请你一定答应我一个请求，\
但愿快一点把药炼成。\

[@talk_3_2]
<$USERNAME>, 药是已经炼成了,\
但是还没有没有解决的部分，\
需要向师傅请教。\
有件事情想求你帮忙，你答应吗？\ \
<接受请求/@ok_h_order>\

[@ok_h_order]
谢谢你，你把这个药送给我师傅刘中医，\
请教师傅这剂药缺少什么东西。\
我不能空着药房，\
所以就请你辛苦一次了。\
但是我师傅好久都没有音信，\
很对不起，请你一定帮我这个忙。\
<接受请求/@ok_h_order_now>\

[@ok_h_order_now]
#act
set  [108] 1
give 许中医的药1 1
close

;-------------------------------------------------------
;
;-------------------------------------------------------


[goods]
;商品          数量       时间
金创药(小量)      200        1
魔法药(小量)      200        1
金创药(中量)  100        1
魔法药(中量)  100        1
强效金创药  100        1
强效魔法药  100        1
