武器
%100
+5
+6
+43

;; 题目 - 决定矿石(命运之刃指示开始)
;#####################################################
;-----------------------------------------------------
;开始指示时 [203]
;执行指示过程中失败或完成时[204]
;惹恼卫家店时 [210]
;惹恼戒指店老板时 [211]
;没有能够调解被惹恼的卫家店心情的金矿时 [212]
;调解好被惹恼的卫家店的心情时[213]
;听到去找铁匠铺老板的话时 [214]
;给卫家店赊账时[215]
;铁匠铺老板请求给高级武士传话时 [216]
;给高级武士说错话时[217]
;承诺打败魔鬼时[218]
;听到有关指示的暗示时 [219]
;打败了魔鬼时 [220]
;打败魔鬼，拿到命运之书时 [221]
;去送命运之书，叫以后再来找时[222]
;---------------------------------------------------
[@main]
#IF
check [204] 0
check [214] 0
check [219] 0
checkitem 金矿 1
#ACT
goto @needgold
break
#IF
random 10
check [204] 0
check [214] 0
check [219] 1
checkitem 金矿 1
#ACT
goto @needgold
break
#IF
unit  [1] 0
check [210] 1
#ACT
goto @notalk
break
#IF
unit  [1] 0
check [203] 0
check [210] 0
check [213] 1
#ACT
goto @kang
break
;;有纯度 15以上的金矿3个以上时开始指示
#IF
unit  [1] 0
check [203] 0
checkitem 金矿 3
checkdura 金矿 15
#ACT
goto @talkwith
#ELSEACT
goto @main_1

;#####################################################

;对话, '*' : 对话tag

[@main_1]
欢迎光临。请问有什么事情帮忙吗？\ \
 <买/@buy>武器\
 <卖/@sell>武器\
 <修理/@repair>武器\
 <询问/@qweapon>有关武器\
 <关闭/@exit>
<>

[@buy]
请挑选要购买的物品。\ \ \
 <返回/@main_1>

[@sell]
请放上去要卖的武器。\ \ \
 <返回/@main_1>

[@repair]
要修理武器吗？\ \
 <返回/@main_1>

[~@repair]
已经修好了。\ \
 <返回/@main_1>

[@qweapon]
店里卖的武器其能力值是定值。\
但是妖魔身上的武器，有时根据不同情况，\
具有特殊功能。\
这种武器拿到店里来，会卖好价钱。\
对了，武器的价格按种类不同，其价格也不同，\
但是大体上，耐力越高越昂贵\ \
 <返回/@main_1>

;-------------------------------------------------------
[@needgold]
#IF
#ACT
set [219] 1
#SAY
你包里发光的是什么？难道是金矿？\
是来卖金矿吗？我最近正在开始利用金矿研究新的武器。\
所以需要很多金矿，不知你能不能帮我找金矿？\
拿金矿来，我会给你好价钱。\ \
 <返回/@main_1>

[@talkwith]
#IF
#ACT
set [219] 0
#SAY
哦, 你今天运气不错。\
那个金矿看上去品质不错。\
但是，今天我没有足够的钱来购买这些金矿，\
不知你能不能给我赊账？\ \
  <免费/@freegive>\
  <赊账/@creditgive>\
  <拒绝/@nogive>\

[@freegive]
#IF
checkitem 金矿 3
checkdura 金矿 10
#ACT
take 金矿 2
takecheckitem
#SAY
啊？你真的把这些白给我？\
真没想到现如今还有你这样的人，看来还有人情啊。\
我不能白拿这么贵重的东西。\
你有没有听说过命运之刃？\
还有在北门街道小店修理武器之后，有没有仔细看过人？\ \
 <返回/@kang>

[@creditgive]
#IF
checkitem 金矿 3
checkdura 金矿 10
#ACT
set [215] 1
take 金矿 2
Takecheckitem
#SAY
嗯，给我赊账？年轻人未免小气了点。\
不过这也算是帮了我，\
我也得告诉你一个对你有用的事情。\
嗯，你听说过命运之刃吗？\
但是我现在比较忙，你去找戒指店老板吧。\ \
 <关闭/@exit>

[@nogive]
什么？不能那样做？哦，看来年轻人常被人骗过啊。\
这么不相信人啊？呵呵。。。\
你如果还这样坚持，以后我也有可能不再和你做交易。\
你还坚持拒绝吗？\ \
 <免费/@freegive>\
 <赊账/@creditgive>\
 <还是拒绝/@r_nogive>

[@r_nogive]
#IF
#ACT
set [210] 1
#SAY
好，随你便吧。\
我也不会再见你第二次!!!\ \
 <关闭/@exit>

[@notalk]
#IF
RANDOM 2
#SAY
见过恼火的铁匠吗？那就是我。\
我脾气很坏，至于这一点你可以问戒指店老板，\
我的臭脾气是出了名的。\
#ELSESAY
我不是说过不再和你这种人来往吗？\
没事了，你走吧。\
#ACT
break

[@kang]
#IF
#ACT
set [214] 1
#SAY
我说的人就是在北门街道铁匠铺做事的铁匠铺老板，\
别看他现在在小店做事没什么出息，\
曾经可是很有名气的铁匠，\
用那时候学到的技术，现在给别人做特殊修理。\
你去找一下他吧，我会事先和他讲好的。\ \
 <关闭/@exit>

[goods]
;商品    数量       时间
木剑       50         1
青铜剑     50         1
铁剑       50         1
匕首       50         1

